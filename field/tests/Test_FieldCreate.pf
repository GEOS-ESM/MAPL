#include "MAPL_TestErr.h"
#include "unused_dummy.H"

module Test_FieldCreate
   use mapl3g_Field_API, only: MAPL_FieldCreate, MAPL_FieldEmptyComplete, MAPL_FieldGet
   use mapl3g_Field_API, only: MAPL_FieldsAreAliased
   use mapl3g_FieldInfo
   use mapl3g_VerticalStaggerLoc, only: VERTICAL_STAGGER_EDGE, VERTICAL_STAGGER_CENTER
   use mapl3g_FieldCondensedArray, only: assign_fptr_condensed_array
   use funit
   use ESMF_TestMethod_mod
   use esmf

   implicit none(type,external)

contains

   ! Just a basic test to ensure that things happen.  Far too many
   ! optional arguments to sensibly test all code paths, but certainly
   ! more tests could be added.
   @test(type=ESMF_TestMethod, npes=[1])
   subroutine test_get_units(this)
      class(ESMF_TestMethod), intent(inout) :: this

      type(ESMF_Field) :: field
      type(ESMF_Geom) :: geom
      type(ESMF_Grid) :: grid
      character(*), parameter :: EXPECTED_UNITS = 'km'
      character(:), allocatable :: units
      integer :: status

      grid = ESMF_GridCreateNoPeriDim(maxIndex=[4,4], _RC)
      geom = ESMF_GeomCreate(grid, _RC)
      
      field = MAPL_FieldCreate(geom, typekind=ESMF_TYPEKIND_R4, units=EXPECTED_UNITS, _RC)

      call MAPL_FieldGet(field, units=units, _RC)
      @assertEqual(units, EXPECTED_UNITS)

      call ESMF_FieldDestroy(field, _RC)
      call ESMF_GridDestroy(grid, _RC)
      call ESMF_GeomDestroy(geom, _RC)

      _UNUSED_DUMMY(this)
   end subroutine test_get_units

   @test(type=ESMF_TestMethod, npes=[1])
   subroutine test_field_create_vert_only(this)
      class(ESMF_TestMethod), intent(inout) :: this

      type(ESMF_Field) :: field
      type(ESMF_Geom) :: geom
      type(ESMF_Grid) :: grid
      real(kind=ESMF_KIND_R4), pointer :: farray(:, :, :)
      integer, parameter :: num_vgrid_levels = 5
      integer :: num_levels, status

      field = ESMF_FieldEmptyCreate(_RC)
      grid = ESMF_GridCreateNoPeriDim(maxIndex=[3,2], _RC)
      geom = ESMF_GeomCreate(grid, _RC)
      call ESMF_FieldEmptySet(field, geom, _RC)
      call MAPL_FieldEmptyComplete( &
           field, &
           typekind=ESMF_TYPEKIND_R4, &
           gridToFieldMap=[0, 0], &
           num_levels=num_vgrid_levels+1, & ! +1 since it's an edge variable
           vert_staggerloc=VERTICAL_STAGGER_EDGE, &
           _RC)

      call MAPL_FieldGet(field, num_levels=num_levels, _RC)
      @assert_that(num_levels, is(num_vgrid_levels+1))
      call assign_fptr_condensed_array(field, farray, _RC)
      @assertEqual(shape(farray), [1, num_vgrid_levels+1, 1])

      call ESMF_FieldDestroy(field, _RC)
      call ESMF_GeomDestroy(geom, _RC)
      call ESMF_GridDestroy(grid, _RC)

      _UNUSED_DUMMY(this)
   end subroutine test_field_create_vert_only

   @test(type=ESMF_TestMethod, npes=[1])
   subroutine test_field_create_3d(this)
      class(ESMF_TestMethod), intent(inout) :: this

      type(ESMF_Field) :: field
      type(ESMF_Geom) :: geom
      type(ESMF_Grid) :: grid
      real(kind=ESMF_KIND_R4), pointer :: farray(:, :, :)
      integer, parameter :: num_vgrid_levels = 5
      integer :: num_levels, status

      field = ESMF_FieldEmptyCreate(_RC)
      grid = ESMF_GridCreateNoPeriDim(maxIndex=[3,2], _RC)
      geom = ESMF_GeomCreate(grid, _RC)
      call ESMF_FieldEmptySet(field, geom, _RC)
      call MAPL_FieldEmptyComplete( &
           field, &
           typekind=ESMF_TYPEKIND_R4, &
           num_levels=num_vgrid_levels, &
           vert_staggerloc=VERTICAL_STAGGER_CENTER, &
           _RC)

      call MAPL_FieldGet(field, num_levels=num_levels, _RC)
      @assertEqual(num_levels, num_vgrid_levels)
      call assign_fptr_condensed_array(field, farray, _RC)
      @assertEqual(shape(farray), [6, num_vgrid_levels, 1])

      call ESMF_FieldDestroy(field, _RC)
      call ESMF_GeomDestroy(geom, _RC)
      call ESMF_GridDestroy(grid, _RC)

      _UNUSED_DUMMY(this)
   end subroutine test_field_create_3d

   @test(type=ESMF_TestMethod, npes=[1])
   subroutine test_self_is_alias(this)
      class(ESMF_TestMethod), intent(inout) :: this

      type(esmf_Field) :: f

      logical :: are_aliased
      integer :: status

      f = esmf_FieldEmptyCreate(_RC)
      are_aliased = mapl_FieldsAreAliased(f, f, _RC) 
      @assert_that(are_aliased, is(true()))
      call esmf_FieldDestroy(f, _RC)
      
   end subroutine test_self_is_alias

   @test(type=ESMF_TestMethod, npes=[1])
   subroutine test_alias_is_alias(this)
      class(ESMF_TestMethod), intent(inout) :: this

      type(esmf_Field) :: f1, f2

      logical :: are_aliased
      integer :: status

      f1 = esmf_FieldEmptyCreate(_RC)
      f2 = esmf_NamedAlias(f1, name='other', _RC) ! is an alias
      are_aliased = mapl_FieldsAreAliased(f1, f2, _RC) 
      @assert_that(are_aliased, is(true()))

      call esmf_FieldDestroy(f1, _RC)

   end subroutine test_alias_is_alias

   @test(type=ESMF_TestMethod, npes=[1])
   subroutine test_nonalias_is_not_alias(this)
      class(ESMF_TestMethod), intent(inout) :: this

      type(esmf_Field) :: f1, f2

      logical :: are_aliased
      integer :: status

      f1 = esmf_FieldEmptyCreate(_RC)
      f2 = esmf_FieldEmptyCreate(_RC) ! not an alias
      are_aliased = mapl_FieldsAreAliased(f1, f2, _RC) 
      @assert_that(are_aliased, is(false()))

      call esmf_FieldDestroy(f1, _RC)
      call esmf_FieldDestroy(f2, _RC)

   end subroutine test_nonalias_is_not_alias

   @test(type=ESMF_TestMethod, npes=[1])
   subroutine test_2_alias_of_original(this)
      class(ESMF_TestMethod), intent(inout) :: this

      type(esmf_Field) :: f1, f2, f3

      logical :: are_aliased
      integer :: status

      f1 = esmf_FieldEmptyCreate(_RC)
      f2 = esmf_NamedAlias(f1, name='other', _RC) ! is an alias
      f3 = esmf_NamedAlias(f1, name='other', _RC) ! is an alias

      are_aliased = mapl_FieldsAreAliased(f2, f3, _RC) 
      @assert_that(are_aliased, is(true()))

      call esmf_FieldDestroy(f1, _RC)

   end subroutine test_2_alias_of_original

   @test(type=ESMF_TestMethod, npes=[1])
   subroutine test_alias_of_alias(this)
      class(ESMF_TestMethod), intent(inout) :: this

      type(esmf_Field) :: f1, f2, f3

      logical :: are_aliased
      integer :: status

      f1 = esmf_FieldEmptyCreate(_RC)
      f2 = esmf_NamedAlias(f1, name='other', _RC) ! is an alias
      f3 = esmf_NamedAlias(f2, name='other', _RC) ! is an alias

      are_aliased = mapl_FieldsAreAliased(f3, f1, _RC) 
      @assert_that(are_aliased, is(true()))

      call esmf_FieldDestroy(f1, _RC)

   end subroutine test_alias_of_alias


end module Test_FieldCreate
