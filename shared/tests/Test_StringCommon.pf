#include "MAPL_TestErr.h"
module Test_StringCommon
   use mapl3g_StringCommon
   use pfunit
   implicit none(type, external)

   ! Common parameters
   character(len=*), parameter :: EMPTY = ''
   character(len=*), parameter :: MIXED = 'AbC_ +09'
   character(len=*), parameter :: ALL_LOWER = 'abc_ +09'
   character(len=*), parameter :: ALL_UPPER = 'ABC_ +09'
   character(len=*), parameter :: DECLARATION = 'When in the course of human events...'

contains
   
   @Test
   subroutine test_to_lower()
      character(len=:), allocatable :: s

      s = to_lower(MIXED)
      ! Verify that mixed case string is converted to all lowercase.
      @assertEqual(ALL_LOWER, s, '"' // s // '" should be all lowercase.')
      ! Verify that it works with an empty string.
      @assertEqual(EMPTY, to_lower(EMPTY), 'String should be empty')

   end subroutine test_to_lower

   @Test
   subroutine test_to_upper()
      character(len=:), allocatable :: s

      s = to_upper(MIXED)
      ! Verify that mixed case string is converted to all uppercase.
      @assertEqual(ALL_UPPER, to_upper(MIXED), '"' // s // '" should be all lowercase.')
      ! Verify that it works with an empty string.
      @assertEqual(EMPTY, to_upper(EMPTY), 'String should be empty.')

   end subroutine test_to_upper

   @Test
   subroutine test_capitalize()
      character(len=*), parameter :: TWOCITIES='it was the best of times....'
      character(len=*), parameter :: CAPITAL='It was the best of times....'
      character(len=*), parameter :: NUMBERED = '1. ' // TWOCITIES
      character(len=:), allocatable :: s

      s = capitalize(TWOCITIES)
      ! Verify that it returns a capitalized string when the first character is a letter.
      @assertEqual(CAPITAL, s, 'The first letter of "' // s // '" should be uppercase.')

      s = capitalize(NUMBERED)
      ! Verify that it returns the same string when the first character is not a letter.
      @assertEqual(NUMBERED, s, 'The fourth letter of "' // s // '" should not be uppercase.')

   end subroutine test_capitalize
   
   @Test
   subroutine test_is_alpha()
      character(len=*), parameter :: LETTERS = 'amzAMZ'
      character(len=*), parameter :: NOT_LETTERS = ' _059=~'
      integer :: i
      character :: c

      ! Verify these are letters.
      do i=1, len(LETTERS)
         c = LETTERS(i:i)
         @assertTrue(is_alpha(c), c // ' is a letter.')
      end do
      ! Verify these are not letters.
      do i=1, len(NOT_LETTERS)
         c = NOT_LETTERS(i:i)
         @assertFalse(is_alpha(c), c // ' is not a letter.')
      end do

   end subroutine test_is_alpha
   
   @Test
   subroutine test_is_alpha_only()
      character(len=*), parameter :: GOOD_STRING = 'String'
      character(len=*), parameter :: NOT_LETTERS = ' _1'
      character(len=:), allocatable :: s
      integer :: i, j

      s = GOOD_STRING
      ! Verify this string contains only letters.
      @assertTrue(is_alpha_only(s), '"' // s // '" contains only letters.')
      ! Verify these strings do not contain letters.
      @assertFalse(is_alpha_only(''), 'The empty string contains no letters.')
      do i=1, len(NOT_LETTERS)
         s = NOT_LETTERS(i:i)
         @assertFalse(is_alpha_only(s), '"' // s // '" contains no letters.')
      end do
      ! Verify these strings contain more than letters.
      do i=1, len(NOT_LETTERS)
         do j=1, len(NOT_LETTERS)
            s = NOT_LETTERS(i:i) // GOOD_STRING // NOT_LETTERS(j:j)
            @assertFalse(is_alpha_only(s), '"' // s // '" contains characters that are not letters.')
         end do
         s = NOT_LETTERS(i:i) // GOOD_STRING
         @assertFalse(is_alpha_only(s), '"' // s // '" contains characters that are not letters.')
      end do
      do j=1, len(NOT_LETTERS)
         s = GOOD_STRING // NOT_LETTERS(j:j)
         @assertFalse(is_alpha_only(s), '"' // s // '" contains characters that are not letters.')
      end do

   end subroutine test_is_alpha_only

   @Test
   subroutine test_is_numeric()
      character(len=*), parameter :: NUMBERS = '0123456789'
      character(len=:), allocatable :: s
      character(len=*), parameter :: NOT_NUMBERS = ' _A'
      integer :: i, j

      ! Verify this string only contains digits.
      s = NUMBERS
      @assertTrue(is_numeric(s), '"'// s //'" contains only digits.')
      ! Verify these strings contain no digits.
      @assertFalse(is_numeric(EMPTY), 'The empty string contains no digits.')
      do i=1, len(NOT_NUMBERS)
         s = NOT_NUMBERS(i:i)
         @assertFalse(is_numeric(s), '"'// s //'" contains no digits.')
      end do
      do i=1, len(NOT_NUMBERS)
         do j=1, len(NOT_NUMBERS)
            s = NOT_NUMBERS(i:i) // NUMBERS // NOT_NUMBERS(j:j)
            @assertFalse(is_numeric(s), '"'// s //'" contains characters that are not digits.')
         end do
         s = NOT_NUMBERS(i:i) // NUMBERS
         @assertFalse(is_numeric(s), '"'// s //'" contains characters that are not digits.')
      end do
      do j=1, len(NOT_NUMBERS)
         s = NUMBERS // NOT_NUMBERS(j:j)
         @assertFalse(is_numeric(s), '"'// s //'" contains characters that are not digits.')
      end do

   end subroutine test_is_numeric

   @Test
   subroutine test_is_alphanumeric()
      character(len=:), allocatable :: s

      s = 'A0_'
      ! Verify this string is contains letters, digits or underscore only.
      @assertTrue(is_alphanumeric(s), '"' // s // '" is alphanumeric (including underscore.)')
      ! Verify this string contains characters that are not letters or numbers.
      @assertFalse(is_alphanumeric(s, exclude_underscore=.TRUE.), '"' // s // '" is not alphanumeric (excluding underscore.)')
      ! Verify these strings contains characters other than letters, digits, or underscore.
      @assertFalse(is_alphanumeric(''), 'The empty string is not alphanumeric.')
      @assertFalse(is_alphanumeric(' '), '<SPACE> is not alphanumeric.')
      @assertFalse(is_alphanumeric('+'), '+ is not alphanumeric.')

   end subroutine test_is_alphanumeric

   @Test
   subroutine test_to_character_array()
      character, allocatable :: chars(:)
      integer :: i
      character :: c, s

      chars=to_character_array(DECLARATION)
      ! Verify the size of the returned character array is the same as the length of the string.
      @assertEqual(size(chars), len(DECLARATION), 'The array size should equal the string length.')
      do i=1, len(DECLARATION)
         c = chars(i)
         s = DECLARATION(i:i)
         ! Verify character i in the array matches character i in the string.
         @assertEqual(s, c, '"' // c // '" does not match "' // s // '".')
      end do
      ! Verify that it works with a zero-length string.
      @assertEqual(0, size(to_character_array(EMPTY)), 'The character array should be size 0.')

   end subroutine test_to_character_array
   
   @Test
   subroutine test_to_string()
      character, allocatable :: chars(:)
      character(len=:), allocatable :: string

      ! This test depends on the to_character_array function.
      ! Check the result of the to_character_array function test to verify this test result is valid.
      chars=to_character_array(DECLARATION)
      string = to_string(chars)
      ! Verify that the returned string is the same as the string used to generate the character array.
      @assertEqual(DECLARATION, string, '"' // string // '" does not match "' // DECLARATION // '".')
      ! Verify that it works with a size zero character array.
      chars = [character :: ]
      @assertEqual(0, len(to_string(chars)), 'The string should have length 0.')

      @assertEqual(0, len(to_string(to_character_array(EMPTY))), 'The returned string should have length 0.')

   end subroutine test_to_string
   
   @Test
   subroutine test_lowercase()
      character(len=*), parameter :: EXPECTED = ALL_LOWER
      character(len=*), parameter :: TEST = MIXED
      integer :: i
      character :: e, a

      do i=1, len(TEST)
         e = EXPECTED(i:i)
         a = lowercase(TEST(i:i))
         ! Verify that each character is converted to lowercase correctly.
         @assertEqual(e, a, '"' // a // '" does not match "' // e // '".')
      end do
      ! Verify that it works with a size zero array.
      @assertEqual(0, size(lowercase([character::])), 'The returned array should have size 0.')

   end subroutine test_lowercase

   @Test
   subroutine test_uppercase()
      character(len=*), parameter :: EXPECTED = ALL_UPPER
      character(len=*), parameter :: TEST = MIXED
      integer :: i
      character :: e, a

      do i=1, len(TEST)
         e = EXPECTED(i:i)
         a = uppercase(TEST(i:i))
         ! Verify that each character is converted to uppercase correctly.
         @assertEqual(e, a, '"' // a // '" does not match "' // e // '".')
      end do
      ! Verify that it works with a size zero array.
      @assertEqual(0, size(uppercase([character::])), 'The returned array should have size 0.')

   end subroutine test_uppercase

   @Test
   subroutine test_is_digit()
      integer :: i
      character(len=*), parameter :: DIGITS = '01234567890'
      character :: c
      ! No magic numbers!
      integer, parameter :: ASCII_MIN=0
      integer, parameter :: ASCII_MAX=127

      ! Verify these are digits.
      do i=1, len(DIGITS)
         @assertTrue(is_digit(DIGITS(i:i)),  DIGITS(i:i) // ' is a digit.')
      end do

      ! Verify these are not digits.
      do i=ASCII_MIN, iachar('0')-1
         c = char(i)
         @assertFalse(is_digit(c),  c // ' is not a digit.')
      end do

      do i=iachar('9')+1, ASCII_MAX
         c = char(i)
         @assertFalse(is_digit(c),  c // ' is not a digit.')
      end do

   end subroutine test_is_digit

   @Test
   subroutine test_get_ascii_range()
      integer, parameter :: RANGE_SIZE = 16
      character(len=RANGE_SIZE) :: string
      character :: char_array(RANGE_SIZE)
      integer :: i, j, k
      character, parameter :: chars(*) = [' ', '0', '@', 'P', '`', 'o']
      character :: ca, cb, cc

      do i=1, len(chars)
         ca = chars(i)
         cb = achar(iachar(ca)+RANGE_SIZE-1)
         char_array = get_ascii_range([ca, cb])
         string = get_ascii_range(ca//cb)
         ! Verify that the ranges, character array and string, match the ranges requested.
         do j=0, RANGE_SIZE-1
            k=j+1
            cb=achar(iachar(ca)+j)
            cc = char_array(k)
            @assertEqual(cb, cc, cc // ' should match ' // cb // '.')
            cc = string(k:k)
            @assertEqual(cb, cc, cc // ' should match ' // cb // '.')
         end do
      end do
      
   end subroutine test_get_ascii_range
   
end module Test_StringCommon
