#include "MAPL_TestErr.h"
#include "unused_dummy.H"

module Test_Couplers
   !use mapl3g_StateItem, only: MAPL_STATEITEM_FIELD
   use mapl3g_StateItem
   use mapl3g_VariableSpec, only: VariableSpec, make_VariableSpec
   use mapl3g_StateItemSpec, only: StateItemSpec
   use mapl3g_StateRegistry, only: StateRegistry
   use mapl3g_UnitsAspect, only: UnitsAspect
   use mapl3g_StateItemAspect, only: StateItemAspect
   use mapl3g_VirtualConnectionPt, only: VirtualConnectionPt
   use mapl3g_StateItemExtension, only: StateItemExtension
   use mapl3g_AspectId, only: UNITS_ASPECT_ID
   use mapl3g_Geom_API
   use mapl3g_VerticalGrid_API
   use pfunit
   use ESMF_TestMethod_mod
   use esmf
   !implicit none(type, external)
   implicit none

   type(ESMF_Geom) :: geom
   class(VerticalGrid), allocatable :: vertical_grid

contains

   @Before
   subroutine setUp(this)
      class(ESMF_TestMethod), intent(inout) :: this
      type(ESMF_HConfig) :: hconfig
      type(MaplGeom) :: mapl_geom
      type(GeomManager), pointer :: geom_mgr
      type(BasicVerticalGridSpec) :: vspec
      type(BasicVerticalGridFactory) :: factory
      integer :: status

      hconfig = ESMF_HConfigCreate(content="{class: latlon, im_world: 12, jm_world: 13, pole: PC, dateline: DC}", _RC)
      geom_mgr => get_geom_manager()
      mapl_geom = geom_mgr%get_mapl_geom(hconfig, _RC)
      geom = mapl_geom%get_geom()

      if(allocated(vertical_grid)) deallocate(vertical_grid)
      vspec = BasicVerticalGridSpec(num_levels=5)
      vertical_grid = factory%create_grid_from_spec(vspec, _RC)
      _UNUSED_DUMMY(this)
      
   end subroutine setUp
   
   @After
   subroutine shutDown(this)
      class(ESMF_TestMethod), intent(inout) :: this

      call ESMF_GeomDestroy(geom)
      _UNUSED_DUMMY(this)

   end subroutine shutDown

   @Test(type=ESMF_TestMethod, npes=[1])
   subroutine test_units(this)
      class(ESMF_TestMethod), intent(inout) :: this
      type(ESMF_StateItem_Flag), parameter :: ITEMTYPE = MAPL_STATEITEM_FIELD
      character(len=*), parameter :: EXPORT_NAME = 'EXPORT_NAME'
      character(len=*), parameter :: IMPORT_NAME = 'IMPORT_NAME'
      type(ESMF_TypeKind_Flag), parameter :: TYPEKIND = ESMF_TYPEKIND_R4 
      character(len=*), parameter :: EXPORT_UNITS = 'm s-1'
      character(len=*), parameter :: IMPORT_UNITS = 'km s-1'
      type(StateRegistry), target :: registry
      type(StateRegistry), pointer :: regptr
      type(VariableSpec) :: var_spec
      type(StateItemSpec) :: export_spec, import_spec
      type(VirtualConnectionPt) :: virtual_pt
      type(StateItemExtension), pointer :: extension
      type(StateItemSpec), pointer :: new_spec
      class(StateItemAspect), pointer :: aspect
      character(len=:), allocatable :: units
      integer :: status

      ! VerticalGrid should be allocated in @Before subroutine
      @assertTrue(allocated(vertical_grid), 'The VerticalGrid has not been allocated.')

      registry = StateRegistry('StateRegistry')
      regptr => registry

      ! Make VariableSpec and make export/import StateItemSpec's
      var_spec = make_VariableSpec(ESMF_STATEINTENT_EXPORT, EXPORT_NAME,&
         & typekind=TYPEKIND, itemtype=ITEMTYPE, units=EXPORT_UNITS, _RC)
      export_spec = var_spec%make_StateItemSpec(regptr, component_geom=geom,&
         & vertical_grid=vertical_grid, _RC)
      call export_spec%create(_RC)

      var_spec = make_VariableSpec(ESMF_STATEINTENT_IMPORT, IMPORT_NAME,&
         & typekind=TYPEKIND, itemtype=ITEMTYPE, units=IMPORT_UNITS, _RC)
      import_spec = var_spec%make_StateItemSpec(regptr, component_geom=geom,&
         & vertical_grid=vertical_grid, _RC)
      call import_spec%create(_RC)

      virtual_pt = VirtualConnectionPt(state_intent='export', short_name=EXPORT_NAME)
      call registry%add_primary_spec(virtual_pt=virtual_pt, spec=export_spec, _RC)

      ! Extend to import StateItemSpec
      extension => registry%extend(virtual_pt, import_spec, _RC)
      new_spec => extension%get_spec()

      ! Compare extension StateItemSpec units to import StateItemSpec units
      aspect => new_spec%get_aspect(UNITS_ASPECT_ID, _RC)
      select type(aspect)
      type is (UnitsAspect)
         units = aspect%get_units()
      end select
      @assertEqual(IMPORT_UNITS, units)
      _UNUSED_DUMMY(this)

   end subroutine test_units

end module Test_Couplers
