#include "MAPL_TestErr.h"
#include "unused_dummy.H"

module Test_Couplers
   use mapl3g_StateItemAspect, only: StateItemAspect
   use mapl3g_TypekindAspect
   use mapl3g_UnitsAspect, only: UnitsAspect
   use mapl3g_StateItem
   use mapl3g_VariableSpec, only: VariableSpec, make_VariableSpec
   use mapl3g_StateItemSpec, only: StateItemSpec
   use mapl3g_StateRegistry, only: StateRegistry
   use mapl3g_VirtualConnectionPt, only: VirtualConnectionPt
   use mapl3g_StateItemExtension, only: StateItemExtension
   use mapl3g_AspectId
   use mapl3g_Geom_API
   use mapl3g_VerticalGrid_API
   use pfunit
   use ESMF_TestMethod_mod
   use esmf
   
   implicit none

   type(ESMF_StateItem_Flag), parameter :: ITEMTYPE = MAPL_STATEITEM_FIELD
   character(len=*), parameter :: EXPORT_NAME = 'EXPORT'
   character(len=*), parameter :: IMPORT_NAME = 'IMPORT'
   type(ESMF_TypeKind_Flag), parameter :: EXPORT_TYPEKIND = ESMF_TYPEKIND_R4
   type(ESMF_TypeKind_Flag), parameter :: IMPORT_TYPEKIND = ESMF_TYPEKIND_R8
   character(len=*), parameter :: EXPORT_UNITS = 'm s-1'
   character(len=*), parameter :: IMPORT_UNITS = 'km s-1'
   character(len=*), parameter :: HCONFIG_CONTENT = &
      & "{class: latlon, im_world: 12, jm_world: 13, pole: PC, dateline: DC}"
   class(VerticalGrid), pointer :: vertical_grid => null()
   type(MaplGeom), pointer :: mapl_geom => null()
   type(GeomManager), pointer :: geom_mgr => null()

contains

   @Test(type=ESMF_TestMethod, npes=[1])
   subroutine test_units(this)
      class(ESMF_TestMethod), intent(inout) :: this
      type(StateRegistry), target :: registry
      type(VariableSpec) :: var_spec
      type(StateItemSpec) :: export_spec, import_spec
      type(VirtualConnectionPt) :: virtual_pt
      type(StateItemExtension), pointer :: extension
      type(StateItemSpec), pointer :: new_spec
      class(StateItemAspect), pointer :: aspect
      character(len=:), allocatable :: units
      type(ESMF_Geom) :: my_geom
      integer :: status

      ! VerticalGrid should be associated in @Before subroutine
      @assertTrue(associated(vertical_grid), 'The VerticalGrid pointer is not associated.')

      my_geom = get_geom()

      registry = StateRegistry('StateRegistry')

      ! Make VariableSpec and make export/import StateItemSpec's
      var_spec = make_VariableSpec(ESMF_STATEINTENT_EXPORT, EXPORT_NAME,&
         & typekind=EXPORT_TYPEKIND, itemtype=ITEMTYPE, units=EXPORT_UNITS, _RC)
      export_spec = var_spec%make_StateItemSpec(registry, component_geom=my_geom,&
         & vertical_grid=vertical_grid, _RC)
      call export_spec%create(_RC)

      var_spec = make_VariableSpec(ESMF_STATEINTENT_IMPORT, IMPORT_NAME,&
         & typekind=EXPORT_TYPEKIND, itemtype=ITEMTYPE, units=IMPORT_UNITS, _RC)
      import_spec = var_spec%make_StateItemSpec(registry, component_geom=my_geom,&
         & vertical_grid=vertical_grid, _RC)
      call import_spec%create(_RC)

      virtual_pt = VirtualConnectionPt(state_intent='export', short_name=EXPORT_NAME)
      call registry%add_primary_spec(virtual_pt=virtual_pt, spec=export_spec, _RC)

      ! Extend to import StateItemSpec
      extension => registry%extend(virtual_pt, import_spec, _RC)
      new_spec => extension%get_spec()

      ! Compare extension StateItemSpec units to import StateItemSpec units
      aspect => new_spec%get_aspect(UNITS_ASPECT_ID, _RC)
      select type(aspect)
      type is (UnitsAspect)
         units = aspect%get_units()
         @assertEqual(IMPORT_UNITS, units)
      end select
      _UNUSED_DUMMY(this)

   end subroutine test_units

   @Test(type=ESMF_TestMethod, npes=[1])
   subroutine test_typekind(this)
      class(ESMF_TestMethod), intent(inout) ::  this
      type(StateRegistry), target :: registry
      type(VariableSpec) :: var_spec
      type(StateItemSpec) :: export_spec, import_spec
      type(VirtualConnectionPt) :: virtual_pt
      type(StateItemExtension), pointer :: extension
      type(StateItemSpec), pointer :: new_spec
      class(StateItemAspect), pointer :: aspect
      type(ESMF_TypeKind_Flag) :: typekind
      logical :: same_typekind
      integer :: status
      type(ESMF_Geom) :: my_geom

      ! VerticalGrid should be associated in @Before subroutine
      @assertTrue(associated(vertical_grid), 'The VerticalGrid pointer is not associated.')

      my_geom = get_geom()

      registry = StateRegistry('StateRegistry')

      ! Make VariableSpec and make export/import StateItemSpec's
      var_spec = make_VariableSpec(ESMF_STATEINTENT_EXPORT, EXPORT_NAME,&
         & typekind=EXPORT_TYPEKIND, itemtype=ITEMTYPE, units="m", _RC)
      export_spec = var_spec%make_StateItemSpec(registry, component_geom=my_geom,&
         & vertical_grid=vertical_grid, _RC)
      call export_spec%create(_RC)

      var_spec = make_VariableSpec(ESMF_STATEINTENT_IMPORT, IMPORT_NAME,&
         & typekind=IMPORT_TYPEKIND, itemtype=ITEMTYPE, _RC)
      import_spec = var_spec%make_StateItemSpec(registry, component_geom=my_geom,&
         & vertical_grid=vertical_grid, _RC)
      call import_spec%create(_RC)

      virtual_pt = VirtualConnectionPt(state_intent='export', short_name=EXPORT_NAME)
      call registry%add_primary_spec(virtual_pt=virtual_pt, spec=export_spec, _RC)

      ! Extend to import StateItemSpec
      extension => registry%extend(virtual_pt, import_spec, _RC)
      new_spec => extension%get_spec()

      ! Compare extension StateItemSpec units to import StateItemSpec units
      aspect => new_spec%get_aspect(TYPEKIND_ASPECT_ID, _RC)
      select type(aspect)
      type is (TypekindAspect)
         typekind = aspect%get_typekind()
         same_typekind = typekind == IMPORT_TYPEKIND
         @assertTrue(same_typekind, "The typekind for the extended typekind is incorrect.")
      end select
      _UNUSED_DUMMY(this)

   end subroutine test_typekind

   @Before
   subroutine setUp(this)
      class(ESMF_TestMethod), intent(inout) :: this
      type(BasicVerticalGridSpec) :: vspec
      type(VerticalGridManager), pointer :: vgrid_manager
      type(ESMF_HConfig) :: hconfig
      integer :: status

      vgrid_manager => get_vertical_grid_manager(_RC)
      vspec = BasicVerticalGridSpec(num_levels=5)
      vertical_grid => vgrid_manager%create_grid(vspec, _RC)

      if(.not. associated(mapl_geom)) then
         hconfig = ESMF_HConfigCreate(content=HCONFIG_CONTENT, _RC)
         mapl_geom => get_mapl_geom_ptr(hconfig)
      end if
      @assertTrue(associated(mapl_geom), 'MaplGeom was not retrieved.')
      _UNUSED_DUMMY(this)

   end subroutine setUp

   @After
   subroutine shutDown(this)
      class(ESMF_TestMethod), intent(inout) :: this

      _UNUSED_DUMMY(this)

   end subroutine shutDown

   type(ESMF_Geom) function get_geom()
      get_geom = mapl_geom%get_geom()
   end function get_geom

   function get_mapl_geom_ptr(hconfig) result(ptr)
      type(MaplGeom), pointer :: ptr
      type(ESMF_HConfig), intent(inout) :: hconfig
      integer :: rc

      ptr => null()
      if(.not. associated(geom_mgr)) geom_mgr => get_geom_manager()
      ptr => geom_mgr%get_mapl_geom(hconfig, rc=rc)
      if(rc /= _SUCCESS) ptr => null()

   end function get_mapl_geom_ptr

end module Test_Couplers
