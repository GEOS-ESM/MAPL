module Test_RunChild
   use mapl3g_GenericGridComp
   use mapl3g_Generic
   use mapl3g_OuterMetaComponent
   use esmf
   use pfunit
   use yafyaml
   use scratchpad, only: log, clear_log
   implicit none

   type(ESMF_GridComp) :: parent_gc
   type(OuterMetaComponent), pointer :: parent_meta
   
contains

   ! Build a parent gc with 2 children.
   subroutine setup(this, rc)
      class(MpiTestMethod), intent(inout) :: this
      integer, intent(out) :: rc

      type(Parser) :: p
      class(YAML_Node), allocatable :: config

      integer :: status

      p = Parser('core')

      config = p%load(TextStream('setServices: {sharedObj: libsimple_parent_gridcomp, userRoutine: setservices_}'))
      parent_gc = create_grid_comp('parent', config, rc=status)
      if (status /= 0) then
         rc = status
         return
      end if

      config = p%load(TextStream('setServices: {sharedObj: libsimple_leaf_gridcomp, userRoutine: setservices_}'))
      parent_meta => get_outer_meta(parent_gc, rc=status)
      if (status /= 0) then
         rc = status
         return
      end if

      call parent_meta%add_child('child_1', config, rc=status)
      if (status /= 0) then
         rc = status
         return
      end if
      call parent_meta%add_child('child_2', config, rc=status)
      if (status /= 0) then
         rc = status
         return
      end if

      call ESMF_GridCompSetServices(parent_gc, setServices, rc=status)
      if (status /= 0) then
         rc = status
         return
      end if
      call clear_log()

      rc = ESMF_SUCCESS
   end subroutine setup

   subroutine teardown(this)
      class(MpiTestMethod), intent(inout) :: this

      call ESMF_GridCompDestroy(parent_gc)
   end subroutine teardown
      

   @test(npes=[0])
   subroutine test_MAPL_Run_child(this)
      class(MpiTestMethod), intent(inout) :: this
      type(ESMF_State) :: importState, exportState
      type(ESMF_Clock) :: clock

      integer :: status

      call setup(this, rc=status)
      @assert_that(status, is(0))

      call MAPL_run_child(parent_gc, child_name='child_1', clock=clock, rc=status)
      @assert_that(status, is(0))
      @assertEqual("wasRun_child_1", log)

      call teardown(this)
      
   end subroutine test_MAPL_Run_child

   @test(npes=[0])
   subroutine test_MAPL_Run_child_other_phase(this)
      class(MpiTestMethod), intent(inout) :: this
      type(ESMF_State) :: importState, exportState
      type(ESMF_Clock) :: clock

      integer :: status

      call setup(this, rc=status)
      @assert_that(status, is(0))

      call MAPL_run_child(parent_gc, child_name='child_1', clock=clock, phase_name='extra', rc=status)
      @assert_that(status, is(0))
      @assertEqual("wasRun_extra_child_1", log)

      call teardown(this)
      
   end subroutine test_MAPL_Run_child_other_phase

   @test(npes=[0])
   subroutine test_add_child_wasrun(this)
      class(MpiTestMethod), intent(inout) :: this
      type(ESMF_State) :: importState, exportState
      type(ESMF_Clock) :: clock

      integer :: status

      call setup(this, rc=status)
      @assert_that(status, is(0))

      call MAPL_run_child(parent_gc, child_name='child_1', clock=clock, rc=status)
      @assert_that(status, is(0))
      @assertEqual("wasRun_child_1", log)

      call teardown(this)
      
   end subroutine test_add_child_wasrun

      
   @test(npes=[0])
   subroutine test_init_children(this)
      class(MpiTestMethod), intent(inout) :: this
      type(ESMF_State) :: importState, exportState
      type(ESMF_Clock) :: clock

      integer :: status

      call setup(this, rc=status)
      @assert_that(status, is(0))

      call parent_meta%initialize(importState, exportState, clock, rc=status)
      @assert_that(status, is(0))
      @assertEqual("wasInit :: wasInit_child_1 :: wasInit_child_2", log)

      call teardown(this)
      
   end subroutine test_init_children

   @test(npes=[0])
   subroutine test_finalize_children(this)
      class(MpiTestMethod), intent(inout) :: this
      type(ESMF_State) :: importState, exportState
      type(ESMF_Clock) :: clock

      integer :: status


      call setup(this, rc=status)
      @assert_that(status, is(0))

      call parent_meta%finalize(importState, exportState, clock, rc=status)
      @assert_that(status, is(0))
      @assertEqual("wasFinal :: wasFinal_child_1 :: wasFinal_child_2", log)

      call teardown(this)

   end subroutine test_finalize_children

end module Test_RunChild
