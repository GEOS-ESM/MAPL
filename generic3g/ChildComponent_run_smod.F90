#include "MAPL_ErrLog.h"

submodule(mapl3g_ChildComponent) ChildComponent_run_smod
   use :: mapl_ErrorHandling
   use :: mapl3g_OuterMetaComponent
   use :: mapl3g_MethodPhasesMapUtils
   use :: mapl_KeywordEnforcer
   implicit none

contains

   module subroutine run_self(this, clock, unusable, phase_idx, rc)
      use mapl3g_OuterMetaComponent, only: get_outer_meta
      use mapl3g_OuterMetaComponent, only: OuterMetaComponent
      class(ChildComponent), intent(inout) :: this
      type(ESMF_Clock), intent(inout) :: clock
      class(KeywordEnforcer), optional, intent(in) :: unusable
      integer, optional, intent(in) :: phase_idx
      integer, optional, intent(out) :: rc

      integer :: status, userRC
      integer :: phase
      type(OuterMetaComponent), pointer :: outer_meta

      outer_meta => get_outer_meta(this%gridcomp, _RC)

      associate ( &
           importState => this%states%importState, &
           exportState => this%states%exportState)

        call ESMF_GridCompRun(this%gridcomp, &
             importState=importState, &
             exportState=exportState, &
             clock=clock, &
             phase=phase_idx, userRC=userRC, _RC)
        _VERIFY(userRC)
      end associate

      _RETURN(_SUCCESS)
      _UNUSED_DUMMY(unusable)
   end subroutine run_self

   recursive module subroutine initialize_self(this, clock, unusable, phase_idx, rc)
      use mapl3g_OuterMetaComponent, only: get_outer_meta
      use mapl3g_OuterMetaComponent, only: OuterMetaComponent
      use mapl3g_GenericGridComp
      class(ChildComponent), intent(inout) :: this
      type(ESMF_Clock), intent(inout) :: clock
      class(KeywordEnforcer), optional, intent(in) :: unusable
      integer, optional, intent(in) :: phase_idx
      integer, optional, intent(out) :: rc

      integer :: status, userRC
      type(OuterMetaComponent), pointer :: outer_meta

      outer_meta => get_outer_meta(this%gridcomp, _RC)

      associate ( &
           importState => this%states%importState, &
           exportState => this%states%exportState)

        call ESMF_GridCompInitialize(this%gridcomp, &
             importState=importState, exportState=exportState, clock=clock, &
             phase=phase_idx, userRC=userRC, _RC)
        _VERIFY(userRC)

      end associate

      _RETURN(_SUCCESS)
      _UNUSED_DUMMY(unusable)
   end subroutine initialize_self

   module subroutine finalize_self(this, clock, unusable, phase_name, rc)
      use mapl3g_OuterMetaComponent, only: get_outer_meta
      use mapl3g_OuterMetaComponent, only: OuterMetaComponent
      class(ChildComponent), intent(inout) :: this
      type(ESMF_Clock), intent(inout) :: clock
      class(KeywordEnforcer), optional, intent(in) :: unusable
      character(len=*), optional, intent(in) :: phase_name
      integer, optional, intent(out) :: rc

      integer :: status, userRC
      integer :: phase
      type(OuterMetaComponent), pointer :: outer_meta

      outer_meta => get_outer_meta(this%gridcomp, _RC)
      phase = get_phase_index(outer_meta%get_phases(ESMF_METHOD_FINALIZE), phase_name=phase_name, _RC)

      associate ( &
           importState => this%states%importState, &
           exportState => this%states%exportState)

        call ESMF_GridCompFinalize(this%gridcomp, &
             importState=importState, exportState=exportState, clock=clock, &
             phase=phase, userRC=userRC, _RC)
        _VERIFY(userRC)
      end associate

      _RETURN(_SUCCESS)
      _UNUSED_DUMMY(unusable)
   end subroutine finalize_self

   module function get_states(this) result(states)
      type(MultiState) :: states
      class(ChildComponent), intent(in) :: this

      states = this%states
   end function get_states

end submodule ChildComponent_run_smod
