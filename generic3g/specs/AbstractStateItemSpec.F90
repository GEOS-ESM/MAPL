#include "MAPL_Generic.h"

module mapl3g_AbstractStateItemSpec
   use mapl_ErrorHandling
   implicit none
   private

   public :: AbstractStateItemSpec

   type, abstract :: AbstractStateItemSpec
      private

      logical :: active = .false.
      logical :: created = .false.
      logical :: allocated = .false.

   contains

!!$      procedure(I_initialize), deferred :: initialize
      procedure(I_make), deferred :: create
      procedure(I_make), deferred :: destroy
      procedure(I_make), deferred :: allocate

      procedure(I_connect), deferred :: connect_to
      procedure(I_can_connect), deferred :: can_connect_to
      procedure(I_can_connect), deferred :: requires_extension
      procedure(I_make_extension), deferred :: make_extension

      procedure(I_add_to_state), deferred :: add_to_state

      procedure, non_overridable :: set_created
      procedure, non_overridable :: is_created
      procedure, non_overridable :: set_allocated
      procedure, non_overridable :: is_allocated
      procedure, non_overridable :: is_active
      procedure, non_overridable :: set_active

      procedure :: make_action
   end type AbstractStateItemSpec

   abstract interface

!!$      subroutine I_initialize(this, geom, var_spec, unusable, rc)
!!$         use esmf, only: ESMF_Geom
!!$         use mapl3g_VariableSpec, only: VariableSpec
!!$         use mapl_KeywordEnforcer, only: KeywordEnforcer
!!$         import AbstractStateItemSpec
!!$         class(AbstractStateItemSpec), intent(inout) :: this
!!$         type(ESMF_Geom), intent(in) :: geom
!!$         type(VariableSpec), intent(in) :: var_spec
!!$         class(KeywordEnforcer), optional, intent(in) :: unusable
!!$         integer, optional, intent(out) :: rc
!!$      end subroutine I_initialize

      subroutine I_connect(this, src_spec, rc)
         use mapl3g_ConnectionSpec
         import AbstractStateItemSpec
         class(AbstractStateItemSpec), intent(inout) :: this
         class(AbstractStateItemSpec), intent(in) :: src_spec
         integer, optional, intent(out) :: rc
      end subroutine I_connect

      logical function I_can_connect(this, src_spec)
         use mapl3g_ConnectionSpec
         import AbstractStateItemSpec
         class(AbstractStateItemSpec), intent(in) :: this
         class(AbstractStateItemSpec), intent(in) :: src_spec
      end function I_can_connect

      ! Will use ESMF so cannot be PURE
      subroutine I_make(this, rc)
         import AbstractStateItemSpec
         class(AbstractStateItemSpec), intent(inout) :: this
         integer, optional, intent(out) :: rc
      end subroutine I_make

      function I_make_extension(this, src_spec, rc) result(action_spec)
         use mapl3g_AbstractActionSpec
         import AbstractStateItemSpec
         class(AbstractActionSpec), allocatable :: action_spec
         class(AbstractStateItemSpec), intent(in) :: this
         class(AbstractStateItemSpec), intent(in) :: src_spec
         integer, optional, intent(out) :: rc
      end function I_make_extension
         
      subroutine I_add_to_state(this, multi_state, actual_pt, rc)
         use mapl3g_MultiState
         use mapl3g_ActualConnectionPt
!!$         use esmf, only: ESMF_State
         import AbstractStateItemSpec
         class(AbstractStateItemSpec), intent(in) :: this
         type(MultiState), intent(inout) :: multi_state
!!$         type(ESMF_State), intent(inout) :: state
         type(ActualConnectionPt), intent(in) :: actual_pt
!!$         character(*), intent(in) :: short_name
         integer, optional, intent(out) :: rc
      end subroutine I_add_to_state

   end interface

contains
   
!!$   ! Non overridable methods
!!$   ! ------------------------
!!$   
!!$   pure subroutine set_name(this, name)
!!$      class(AbstractStateItemSpec), intent(inout) :: this
!!$      character(*), intent(in) :: name
!!$      this%name = name
!!$   end subroutine set_name
!!$   
!!$
!!$   pure function get_name(this) result(name)
!!$      character(:), allocatable :: name
!!$      class(AbstractStateItemSpec), intent(in) :: this
!!$      name = this%name
!!$   end function get_name
!!$   
!!$   pure subroutine set_ultimate_source_gc(this, ultimate_source_gc)
!!$      class(AbstractStateItemSpec), intent(inout) :: this
!!$      character(*), intent(in) :: ultimate_source_gc
!!$      this%ultimate_source_gc = ultimate_source_gc
!!$   end subroutine set_ultimate_source_gc
!!$   
!!$
!!$   pure function get_ultimate_source_gc(this) result(ultimate_source_gc)
!!$      character(:), allocatable :: ultimate_source_gc
!!$      class(AbstractStateItemSpec), intent(in) :: this
!!$      ultimate_source_gc = this%ultimate_source_gc
!!$   end function get_ultimate_source_gc
!!$   
!!$
   pure subroutine set_allocated(this, allocated)
      class(AbstractStateItemSpec), intent(inout) :: this
      logical, optional, intent(in) :: allocated

      if (present(allocated)) then
         this%allocated = allocated
      else
         this%allocated =  .true.
      end if

   end subroutine set_allocated

   pure logical function is_allocated(this)
      class(AbstractStateItemSpec), intent(in) :: this
      is_allocated = this%allocated
   end function is_allocated

   pure subroutine set_created(this, created)
      class(AbstractStateItemSpec), intent(inout) :: this
      logical, optional, intent(in) :: created

      if (present(created)) then
         this%created = created
      else
         this%created =  .true.
      end if

   end subroutine set_created

   pure logical function is_created(this)
      class(AbstractStateItemSpec), intent(in) :: this
      is_created = this%created
   end function is_created

   pure subroutine set_active(this, active)
      class(AbstractStateItemSpec), intent(inout) :: this
      logical, optional, intent(in) :: active

      if (present(active)) then
         this%active = active
      else
         this%active =  .true.
      end if

   end subroutine set_active

   pure logical function is_active(this)
      class(AbstractStateItemSpec), intent(in) :: this
      is_active = this%active
   end function is_active


   function make_action(this, dst_spec, rc) result(action)
      use mapl3g_ExtensionAction
      class(ExtensionAction), allocatable :: action
      class(AbstractStateItemSpec), intent(in) :: this
      class(AbstractStateItemSpec), intent(in) :: dst_spec
      integer, optional, intent(out) :: rc

      _FAIL('Subclass has not implemented make_action')
   end function make_action

end module mapl3g_AbstractStateItemSpec
